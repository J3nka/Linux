#!/usr/bin/perl -w

use strict;
use Data::Dumper;
use Getopt::Long;
use Term::ANSIColor;

my %options;

Getopt::Long::GetOptions(
    "help"         => sub{help()},

    "commit"       => \$options{"commit"},
    "dir=s"        => \$options{"dir"},
    "noprompt"     => \$options{"noprompt"},
    "renameonpath" => \$options{"renameonpath"},

    "duplicates"   => \$options{"duplicates"},

    "actor=s"      => \$options{"filter"}{"actors"},
    "title=s"      => \$options{"filter"}{"title"},
    "quality=s"    => \$options{"filter"}{"title"},
    "story=s"      => \$options{"filter"}{"story"},
    "filetype=s"   => \$options{"filter"}{"filetype"},
    "source=s"     => \$options{"filter"}{"source"},
    "date=s"       => \$options{"filter"}{"date"},
) or help();

sub help {
print "Usage: $0 <input folder 1> <input folder 2> <...> <options>
--help              Print this helptext

--commit            Move files and clean input directories
--dir               Set a custom output directory(Default is same as input)
--noprompt          Dont prompt for source names
--renameonpath      Rename files based on filepath

--duplicates        Search for duplicates(finds files with same new name or same duration)

--actor             Filter by actors
--title             Filter by title
--quality           Filter byt quality
--story             Filter by story
--filetype          Filter by filetype
--source            Filter by source
--date              Filter by date
";
exit;
}

help() if !@ARGV;
my @LIST;
foreach my $arg (@ARGV) {
    my @filepaths;
    if ($options{"renameonpath"} && $options{"filter"}{"source"}) {
        chomp(@filepaths = `find "$arg/$options{'filter'}{'source'}" -type f`);
    }
    else {
        chomp(@filepaths = `find "$arg" -type f`);
    }
    foreach my $path (@filepaths) {
        $path = {
            filepath => $path,
            inpath   => $arg,
            dir      => $options{"dir"} // $arg,
        };
    }
    push @LIST, @filepaths;
}
my %renamedata;
get_actors(@LIST);
$_ = build_renamehash($_) for @LIST;
@LIST = filter(@LIST);
if ($options{"duplicates"}) {
    @LIST = mediainfo(@LIST);
    my $count = find_duplicates(@LIST);
    foreach my $attribute(keys %$count) {
        foreach my $duplicate ( grep {scalar @{$count->{$attribute}->{$_}} < 2 } keys %{$count->{$attribute}}) {
            delete $count->{$attribute}->{$duplicate};
        }
    }
    print Dumper $count;
    exit;
}
build_movepath(@LIST);
move_clean(@LIST);

sub filter {
    my @LIST = @_;
    foreach my $param (keys %{$options{"filter"}}) {
        if ($options{"filter"}{$param}) {
            @LIST = grep {
                    if ($_->{$param}) {
                        if (ref $_->{$param} eq "ARRAY") {
                            grep { /$options{"filter"}{$param}/i } @{$_->{$param}};
                        }
                        else {
                            $_->{$param} =~ /$options{"filter"}{$param}/i
                        }
                    }
                } @LIST;
        }
    }
    return @LIST;
}

sub get_actors {
    my @LIST = @_;
    foreach my $file (@LIST) {
        my $filename = (split("/",$file->{"filepath"}))[-1];
        my @actors = sort ($filename =~ /\(([^()]+)\)/g);
        push @{$renamedata{"actors"}}, @actors;
    }

    @{$renamedata{"actors"}} = grep { scalar (split(" ",$_)) > 1 } @{$renamedata{"actors"}};
    @{$renamedata{"actors"}} = unique(sort @{$renamedata{"actors"}});
    foreach my $actor (@{$renamedata{"actors"}}) {
        my @names = split (" ", $actor);
        $_ = lc $_ and $_ = ucfirst $_ for @names;
        $actor = join(" ", @names);
    }
}

sub build_renamehash {
    my ($file, %args) = @_;
    my @filesplit = split("/",$file->{"filepath"});
    $file->{"newfile"} = $file->{"orgfile"} = pop @filesplit;
    $file->{"newfile"} =~ s/([\w']+)/\u\L$1/g;
    $file->{"newfile"} =~ s/(\.\w+$)/\L$1/g;
    if ($file->{"newfile"} =~ /\.(\w+)$/) {
        $file->{"filetype"} = $1;
        $file->{"newfile"} =~ s/\.\w+$//;
    }
    else {
        push @{$file->{"error"}}, "No filetype";
    }
    @{$file->{"source"}} = $file->{"orgfile"} =~ /\[([^\[\]]+)\]/g;
    foreach my $source (@{$file->{"source"}}) {
        next if $options{"renameonpath"};
        my $orgsource = $source;
        if (my @match = grep { $_ =~ /^(?:$source)$/i } keys %{$renamedata{"sources"}}) {
            $source = $renamedata{"sources"}{$match[0]};
        }
        else {
            if (!$options{"noprompt"}) {
                print "Enter source [$source]: ";
                chomp ($source = <STDIN>);
            }
            $source = $orgsource if $source eq "";
            $renamedata{"sources"}{$orgsource} = $source;
        }
    }
    $file->{"newfile"} =~ s/\[[^\[\]]+\]//ig;
    if ($renamedata{"actors"}) {
        foreach my $actor(@{$renamedata{"actors"}}) {
            if ($file->{"newfile"} =~ /(^| )\(?$actor\)?($| )/i) {
                $file->{"newfile"} =~ s/(^| )\(?$actor\)?($| )/$1($actor)$2/i;
            }
        }
    }
    @{$file->{"actors"}} = sort ($file->{"newfile"} =~ /\(([^()]+)\)/g);
    $file->{"newfile"} =~ s/\(([^()]+)\)//g;
    my $actors = join("|",@{$file->{"actors"}});
    while ($actors ne "" && $file->{"newfile"} =~ /^\s*($actors)/) {
        $file->{"newfile"} =~ s/^\s*($actors)//;
    }
    @{$file->{"story"}} = $file->{"newfile"} =~ /\{([^{}]+)\}/g;
    $file->{"newfile"} =~ s/\{([^{}]+)\}//g;
    if ($file->{"newfile"} =~ /(^| )(\d{3,4})p($| )/i && !$args{duplicate}) {
        $file->{"quality"} = $2;
    }
    else {
        chomp(my @mediainfo = `ffmpeg -i "$file->{filepath}" 2>&1 | egrep "Duration|Video:"`);
        my @mediainfo2;
        eval {
            no warnings "all";
            @mediainfo2 = grep {/\d{3,4}x\d{3,4}/} split(",",$mediainfo[1]);
            my $resolution = (split("x",$mediainfo2[0]))[1];
            $file->{"quality"} = (split(" ",$resolution))[0] // undef;
        } or push @{$file->{error}}, "Resolution";
        eval {
            no warnings "all";
            @mediainfo2 = grep {/bitrate/} split(",",$mediainfo[0]);
            my ($bitrate) = $mediainfo2[0] =~ /bitrate: (\d+)/;
            $file->{"bitrate"} = $bitrate // undef;
        } or push @{$file->{error}}, "Bitrate";
    }
    $file->{"newfile"} =~ s/(^| )(\d{3,4})p($| )/$1$3/i;
    if ($file->{"newfile"} =~ /(^| )(\d{4}\D\d{2}\D\d{2})($| )/) {
        $file->{"date"} = $2;
        $file->{"date"} =~ s/\D/ /g;
        $file->{"newfile"} =~ s/(^| )(\d{4}\D\d{2}\D\d{2})($| )/$1$3/;
    }
    $file->{"newfile"} =~ s/\s+/ /g;
    $file->{"newfile"} =~ s/^\s+|\s+$//g;
    $file->{"title"} = $file->{"newfile"};
    $file->{"newfile"} = "";
    if ($options{"renameonpath"}) {
        @{$file->{"actors"}} = ();
        @{$file->{"source"}} = ();
        @{$file->{"story"}} = ();
        my ($relativepath) = $file->{"filepath"} =~ /$ARGV[0]\/?(.*)/;
        my @path = split("/",$relativepath);
        pop @path;
        while ($path[0] and $path[0] !~ /^(#Actors|Other)$/) {
            push @{$file->{"source"}}, shift @path;
        }
        while (my $folder = shift @path) {
            if ($folder eq "#Actors") {
                @{$file->{"actors"}} = split(" & ", shift @path);
                next;
            }
            next if $folder eq "Other";
            push @{$file->{"story"}}, $folder;
        }
    }
    my @filename;
    (scalar @{$file->{"source"}} > 0) and push @filename, join("",map { "[$_]" } @{$file->{"source"}});
    (scalar @{$file->{"actors"}} > 0) and push @filename, join(" ",map { "($_)" } sort @{$file->{"actors"}});
    (scalar @{$file->{"story"}} > 0) and push @filename, join(" ",map { "{$_}" } @{$file->{"story"}});
    $file->{"title"} and push @filename, $file->{"title"};
    $file->{"date"} and push @filename, $file->{"date"};
    $file->{"quality"} and push @filename, $file->{"quality"}."p";
    $file->{"newfile"} = join(" ", @filename).".".$file->{"filetype"};
    return $file;
}

sub build_movepath {
    my @LIST = @_;
    foreach my $file (@LIST) {
        my @movepaths;
        my @movepath;
        push @movepath, $file->{"dir"};
        push @movepath, scalar @{$file->{"source"}} > 0? @{$file->{"source"}}:"Other";
        if (scalar @{$file->{"actors"}} > 0) {
            push @movepath, ("#Actors",join(" & ", @{$file->{"actors"}}));
        }
        push @movepath, @{$file->{"story"}};
        push @movepaths, join("/",@movepath);
        @{$file->{"outpaths"}} = @movepaths;
    }
}

sub move_clean {
    my @LIST = @_;
    foreach my $file (grep {!$_->{"error"}} @LIST) {
        foreach my $outpath (@{$file->{"outpaths"}}[0]) {
            if (! -e $outpath && $options{"commit"}) {
                system "mkdir -p -m 700 \"$outpath\"";
            }
            if (! -f $outpath."/".$file->{"newfile"}) {
                if ($options{"commit"}) {
                    system sprintf("chmod 600 \"%s\"", $file->{filepath});
                    system sprintf("mv \"%s\" \"%s\"", $file->{"filepath"}, $outpath."/".$file->{"newfile"});
                }
                printf("Old: \"%s\"\n", $file->{"filepath"});
                printf("New: \"%s\"\n\n", $outpath."/".$file->{"newfile"});
            }
            else {
                if ($file->{"filepath"} ne $outpath."/".$file->{"newfile"}) {
                    my $oldfile = { filepath => $outpath."/".$file->{"newfile"} };
                    $oldfile = build_renamehash($oldfile, duplicate => 1);
                    $file = build_renamehash($file, duplicate => 1);
                    push @{$file->{"error"}}, sprintf("Duplicate: \"%s\"\nResolution(old/new): %s\nBitrate(old/new): %s\n",
                        $outpath."/".$file->{"newfile"},
                        ($oldfile->{quality}//"")."/".($file->{quality}//""),
                        ($oldfile->{bitrate}//"")."/".($file->{bitrate}//"")
                    );
                }
            }
        }
    }
    foreach my $file (grep {$_->{"error"}} @LIST) {
        printf("\"%s\"\n%s\n", colored($file->{filepath}, "red"), join("\n",@{$file->{"error"}}));
    }
    foreach (@ARGV) {
        system sprintf("find %s -empty -delete", $_) if $options{commit};
    }
    print $options{commit}? colored("Moved files and cleaned old folders\n","green"):colored("No changes made\n","yellow");
}

sub mediainfo {
    my @LIST = @_;
    foreach my $file (@LIST) {
        chomp(my @videoinfo = `ffmpeg -i "$file->{filepath}" 2>&1 | egrep "Duration|Video:"`);
        foreach my $row (@videoinfo) {
            if ($row =~ /Duration: (\d{2}:\d{2}:\d{2})\.\d{2}.*bitrate (\d+)/) {
                $file->{"duration"} = $1;
                $file->{"bitrate"} = $2;
            }
            if ($row =~ /Video:/) {
                $file->{"resolution"} = (split(",",$row))[2];
            }
        }
    }
    return @LIST;
}

sub find_duplicates {
    my @LIST = @_;
    my $count = {};
    foreach my $file(@LIST) {
        foreach my $attribute(("newfile","duration")) {
            push @{$count->{$attribute}->{$file->{$attribute}}}, $file->{"filepath"} if $file->{$attribute};
        }
    }
    return $count;
}

sub unique {
    my %seen;
    return grep !$seen{$_}++, @_;
}
